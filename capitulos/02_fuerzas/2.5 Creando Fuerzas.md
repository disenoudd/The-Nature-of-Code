# 2.5 Creando Fuerzas

Tomémonos un momento para recordar dónde estamos. Sabemos lo que es una fuerza (un vector), y sabemos cómo aplicar una fuerza a un objeto (dividirlo por la masa y agregarlo al vector de aceleración del objeto). ¿Qué nos falta? Bueno, todavía tenemos que averiguar cómo podemos obtener la fuerza en primer lugar. ¿De dónde vienen las fuerzas?

En este capítulo, veremos dos métodos para crear fuerzas en nuestro mundo de Processing.

1. **¡Inventa una fuerza!** Después de todo, eres un programador, el creador de tu mundo. No hay ninguna razón por la que no puedas inventar una fuerza y aplicarla.
2. **¡Modela una fuerza!** Si, las fuerzas existen en el mundo real. De hecho, los libros de física generalmente contienen fórmulas para esas fuerzas. Podemos tomar esas fórmulas, traducirlas a código fuente, y modelar fuerzas del mundo real en Processing.

La manera más fácil de inventar una fuerza es eligiendo un número. Empecemos con la idea de simular el viento. ¿Qué te parece una fuerza de viento bastante débil y que apunte hacia la derecha? Asumiendo un objeto `Mover` m, nuestro código se vería así:

```javascript
PVector viento = new PVector(0.01,0);
m.applyForce(viento);
```

El resultado no es tan interesante, pero es un buen lugar para empezar. Creamos un objeto `PVector`, lo inicializamos, y lo pasamos a un objeto (el cual lo aplicará a su propia aceleración). Si quisiéramos tener dos fuerzas, por ejemplo viento y gravedad (con mayor intensidad y apuntando hacia abajo), deberíamos escribir lo siguiente:

#### Ejemplo 2.1: Fuerzas

```javascript
PVector viento = new PVector(0.01,0);
PVector gravedad = new PVector(0,0.1);
m.applyForce(viento);
m.applyForce(gravedad);
```

Ahora tenemos dos fuerzas, apuntando hacia distintas direcciones, y con distintas magnitudes, ambas aplicadas al objeto `m`. Estamos empezando a lograr algo. Hemos creado un mundo en Processing para nuestros objetos, un ambiente al cual pueden responder.

Veamos cómo podemos hacer que este ejercicio sea más emocionante con muchos objetos de masa variable. Para hacer esto, necesitaremos un pequeño repaso de programación orientada en el objeto. Nuevamente, no estamos cubriendo todas las bases de programación (para eso, puedes revisar cualquiera de los libros introductorios de Processing listados en la introducción). Como la idea de crear un mundo lleno de objetos es bastante fundamental para todos los ejemplos de este libro, vale la pena que nos tomemos un momento para ir a través de los pasos de pasar de un objeto a muchos.

Aquí es donde estamos con una clase `Mover` como un todo. Notemos que es idéntica a la clase `Mover` creada en el Capítulo 1, con dos adiciones-`masa` y una nueva función de `applyForce()`.

```javascript
class Mover {
  PVector ubicacion;
  PVector velocidad;
  PVector aceleracion;
  float masa;                                                       //¡El objeto ahora tiene masa!
  
  Mover() {
    masa = 1;      //Y por ahora, para hacerlo más fácil, haremos que la masa sea equivalente a 1.
    ubicacion = new PVector(30,30);
    velocidad = new PVector(0,0);
    aceleracion = new PVector(0,0);
  }
  
  void applyForce(PVector fuerza) {                                    //La segunda ley de Newton.
    PVector f = PVector.div(fuerza,masa); //Recibe una fuerza, se divide por la masa, y se le suma a la aceleración.
    aceleracion.add(f);
  }
  
  void actualizar() {
    velocidad.add(aceleracion);                                        //Motion 101 from Chapter 1
    ubicacion.add(velocidad);
    aceleracion.mult(0);                            //Now add clearing the acceleration each time!
  }
  
  void mostrar() {
    stroke(0);
    fill(175);
    ellipse(ubicacion.x,ubicacion.y,masa*16,masa*16);         //Escalando el tamaño según la masa.
  }
  
  void comprobarBordes() { //De alguna manera, estamos decidiendo que un objeto rebota cuando choca con el borde de una ventana
    if (ubicacion.x > width) {
      ubicacion.x = width;
      velocidad.x *= -1;
    } else if (ubicacion.x < 0) {
      velocidad.x *= -1;
      ubicacion.x = 0;
    }
    
    if (ubicacion.y > height) {
      velocidad.y *= -1; //Aunque dijimos que no íbamos a tocar la ubicación y velocidad directamente, hay algunas excepciones. Aquí lo estamos haciendo como una manera fácil y rápida de revertir la dirección de nuestro objeto cuando llega a alguno de los bordes.
      ubicacion.y = height;
    }
  }
}
```

Ahora que nuestra clase está establecida, podemos elegir crear, por ejemplo, cien objetos `Mover` con un array.

```javascript
Mover[] movers = new Mover[100];
```

Después podemos inicializar todos esos objetos `Mover` en el `setup()` con un bucle.

```javascript
void setup() {
  for (int i = 0; i < movers.length; i++) {
    movers[i] = new Mover();
  }
}
```

Ahora tenemos un pequeño problema. Si volvemos hacia atrás y nos referimos al constructor del objeto `Mover`...

```javascript
Mover() {
  masa = 1;                   //Todos los objetos tienen una masa de 1 y una ubicación de (30,30).
  ubicacion = new PVector(30,30);
  velocidad = new PVector(0,0);
  aceleracion = new PVector(0,0);
}
```

…descubriremos que todos los objetos `Mover` están hechos de la misma forma. Lo que queremos son más objetos `Mover` de masa variable y que empiezan en ubicaciones variables. Aquí es donde necesitamos aumentar la sofisticación de nuestro constructor, añadiendo nuevos argumentos.

```javascript
Mover(float m, float x, float y) {
  masa = m;                                    //Ahora establecemos estas variables con argumentos
  ubicacion = new PVector(x,y);
  velocidad = new Pvector(0,0);
  aceleracion = new PVector(0,0);
}
```

Date cuenta de que la masa y la ubicación ya no son establecidas por números, si no que son inicializadas con argumentos pasados a través de un constructor. Esto significa que podemos crear una gran variedad de objetos `Mover`: grandes, pequeños, los que empiezan en el lado izquierdo de la pantalla, los que empiezan en el lado derecho, etc.

```javascript
Mover m1 = new Mover(10,0,height/2);          //Un Mover grande en el lado izquierdo de la ventana
Mover m1 = new Mover(0.1,width,height/2);      //Un Mover pequeño en el lado derecho de la ventana
```

Sin embargo, con un array queremos inicializar todos los objetos con un bucle.

```javascript
void setup() {
  for (int i = 0; i < movers.length; i++) {
    movers[i] = new Mover (random(0.1,5),0,0); //Inicializamos muchos objetos Mover, todos con una masa aleatoria (y todos empezando en 0,0)
  }
}
```

Por cada mover creado, a la masa se le da un valor aleatorio entre 0.1 y 5, y la ubicación inicial en x e y están establecidas a 0. Ciertamente, hay muchas maneras para inicializar los objetos; esta es sólo una demostración de una de ellas.

Una vez que el array de objetos está declarado, creado e inicializado, el resto del código es simple. Corremos sobre cada objeto, les entregamos a cada uno de ellos las fuerzas del ambiente, y disfrutamos del espectáculo.

#### Ejemplo 2.2: Fuerzas actuando en muchos objetos

```javascript
void draw() {
  background(255);
  
  PVector viento = new PVector(0.01,0);
  PVector gravedad = new PVector(0,0.1);                                 //Inventamos dos fuerzas.
  
  for (int i = 0; i < movers.length; i++) { //Bucle a través de todos los objetos, aplicando ambas fuerzas a cada objeto.
    movers[i].applyForce(viento);
    movers[i].applyForce(gravedad);
    movers[i].actualizar();
    movers[i].mostrar();
    movers[i].comprobarBordes();
  }
}
```

 Fíjate en que en la imágen superior los círculos más pequeños llegan al lado derecho de la ventana más rápido que los más grandes. Esto es por nuestra fórmula: *aceleración = fuerza dividida por la masa*. Mientras más grande sea la masa, la aceleración será menor.

#### Ejercicio 2.3

En vez de objetos que reboten fuera de los bordes de la pared, crea un ejemplo en el que una fuerza invisible empuje a los objetos para mantenerlos dentro de la ventana. ¿Puedes pesar la fuerza de acuerdo a qué tan lejos del borde esté el objeto-por ejemplo, mientras más cerca, más intensa será la fuerza?